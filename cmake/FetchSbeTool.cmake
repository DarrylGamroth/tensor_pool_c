function(tp_fetch_sbe_tool out_var)
    if (SBE_TOOL_CACHE_DIR STREQUAL "")
        set(_cache_dir "${CMAKE_CURRENT_BINARY_DIR}/_deps/sbe-all")
    else ()
        set(_cache_dir "${SBE_TOOL_CACHE_DIR}")
    endif ()
    file(MAKE_DIRECTORY "${_cache_dir}")

    set(_artifact "sbe-all")
    set(_groups io.aeron uk.co.real-logic)
    set(_version "${SBE_TOOL_VERSION}")
    set(_group_found "")

    if (_version STREQUAL "")
        foreach (_group IN LISTS _groups)
            string(REPLACE "." "/" _group_path "${_group}")
            set(_meta_url "https://repo1.maven.org/maven2/${_group_path}/${_artifact}/maven-metadata.xml")
            set(_meta_file "${_cache_dir}/${_artifact}-${_group}.xml")
            file(DOWNLOAD "${_meta_url}" "${_meta_file}" STATUS _meta_status LOG _meta_log)
            list(GET _meta_status 0 _meta_code)
            if (NOT _meta_code EQUAL 0)
                continue()
            endif ()

            file(READ "${_meta_file}" _meta_xml)
            string(REGEX MATCH "<release>([^<]+)</release>" _release_match "${_meta_xml}")
            if (CMAKE_MATCH_1)
                set(_version "${CMAKE_MATCH_1}")
                set(_group_found "${_group}")
                break()
            endif ()

            string(REGEX MATCH "<latest>([^<]+)</latest>" _latest_match "${_meta_xml}")
            if (CMAKE_MATCH_1)
                set(_version "${CMAKE_MATCH_1}")
                set(_group_found "${_group}")
                break()
            endif ()
        endforeach ()
    endif ()

    if (_version STREQUAL "")
        message(FATAL_ERROR "Unable to resolve sbe-all version from Maven metadata. Set -DSBE_TOOL_VERSION= or -DSBE_TOOL_JAR=/path/to/jar.")
    endif ()

    set(_jar "${_cache_dir}/${_artifact}-${_version}.jar")
    if (NOT EXISTS "${_jar}")
        if (_group_found STREQUAL "")
            set(_groups_to_try ${_groups})
        else ()
            set(_groups_to_try "${_group_found}")
        endif ()

        foreach (_group IN LISTS _groups_to_try)
            string(REPLACE "." "/" _group_path "${_group}")
            set(_jar_url "https://repo1.maven.org/maven2/${_group_path}/${_artifact}/${_version}/${_artifact}-${_version}.jar")
            set(_jar_tmp "${_jar}.tmp")
            file(DOWNLOAD "${_jar_url}" "${_jar_tmp}" STATUS _jar_status LOG _jar_log)
            list(GET _jar_status 0 _jar_code)
            if (_jar_code EQUAL 0)
                file(RENAME "${_jar_tmp}" "${_jar}")
                set(_group_found "${_group}")
                break()
            endif ()
            file(REMOVE "${_jar_tmp}")
        endforeach ()
    endif ()

    if (NOT EXISTS "${_jar}")
        message(FATAL_ERROR "Unable to download sbe-all ${_version} from Maven. Set -DSBE_TOOL_JAR=/path/to/jar.")
    endif ()

    if (WIN32)
        set(_cp_sep ";")
    else ()
        set(_cp_sep ":")
    endif ()
    set(_classpath "${_jar}")

    set(${out_var} "${_jar}" PARENT_SCOPE)
    set(SBE_TOOL_CLASSPATH "${_classpath}" PARENT_SCOPE)
endfunction()
